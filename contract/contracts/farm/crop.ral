import "std/nft_interface"

Contract Crop(
    collectionId: ByteVec,
    nftIndex: U256,
    name: ByteVec,
    alphAmount: U256,
    mut expires: U256
) extends Constants() implements IDynamicNFT {
    pub fn getTokenUri() -> ByteVec {
        return b`data:application/json,{"name":"` ++ name ++ b`"}`
    }

    pub fn getNFTIndex() -> U256 {
        return nftIndex
    }

    pub fn getCollectionIndex() -> (ByteVec, U256) {
        return collectionId, nftIndex
    }

    pub fn getExpires() -> U256 {
        return expires
    }

    pub fn getAlphAmount() -> U256 {
        return alphAmount
    }

    @using(updateFields = true)
    pub fn setExpires(newExpires: U256) -> () {
        let caller = callerContractId!()
        checkCaller!(collectionId == caller, ErrorCodes.OnlyParentAllowed)
        expires = newExpires
        emit MetadataUpdated()
    }

    @using(assetsInContract = true, preapprovedAssets = true)
    pub fn delete(refundAddress: Address) -> () {
        let caller = callerContractId!()
        let tokenId = selfTokenId!()
        checkCaller!(collectionId == caller, ErrorCodes.OnlyParentAllowed)
        burnToken!(refundAddress, tokenId, 1)
        destroySelf!(refundAddress)
    }

    // DynamicNFT methods

    pub fn getName() -> ByteVec {
        return name
    }

    pub fn getDescription() -> ByteVec {
        return b``
    }

    pub fn getImage() -> ByteVec {
        return b``
    }

    pub fn getTraits() -> [Trait; 2] {
        return [
            Trait {
                traitType: b`Expires`,
                value: u256ToString!(expires)
            },
            Trait {
                traitType: b`ALPH`,
                value: u256ToString!(alphAmount)
            }
        ]
    }

    pub fn getTraitCount() -> U256 {
        return 2
    }

    pub fn getTraitAtIndex(index: U256) -> Trait {
        return getTraits()[index]
    }
}